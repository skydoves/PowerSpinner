// Designed and developed by 2019 skydoves (Jaewoong Eum)
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
//    You may obtain a copy of the License at
//
// http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
//     WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import com.skydoves.powerspinner.Configuration
import com.skydoves.powerspinner.Dependencies

plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.dokka"
    id "binary-compatibility-validator"
}

ext {
    PUBLISH_GROUP_ID = Configuration.artifactGroup
    PUBLISH_ARTIFACT_ID = 'powerspinner'
    PUBLISH_VERSION = rootVersionName
}

apply from: "${rootDir}/scripts/publish-module.gradle"

android {
    compileSdkVersion Configuration.compileSdk
    defaultConfig {
        minSdkVersion Configuration.minSdk
        targetSdkVersion Configuration.targetSdk
        versionCode Configuration.versionCode
        versionName Configuration.versionName
    }

  resourcePrefix 'powerspinner'

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  kotlinOptions.freeCompilerArgs += ["-Xexplicit-api=strict"]
}

apiValidation {
    ignoredPackages += [
        "com/skydoves/powerspinner/databinding",
    ]
    nonPublicMarkers += [
        "kotlin.PublishedApi",
    ]
}

dependencies {
    implementation Dependencies.appcompat
    implementation Dependencies.recyclerView
    implementation Dependencies.preference
    implementation Dependencies.lifecycle
}

apply from: "$rootDir/spotless/spotless.gradle"
